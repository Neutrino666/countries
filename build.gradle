buildscript {
    ext {
        lombokVersion = '1.18.42'
        postgresVersion = '42.7.7'
        flywayVersion = '11.20.0'
        junitVersion = '6.0.0'
        spotbugsVersion = '4.9.8'
    }
}

plugins {
	id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'orq.neutrino'
version = '0.0.1-SNAPSHOT'
description = 'My first spring boot app'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.grpc:spring-grpc-dependencies:0.4.0-SNAPSHOT'
    }
}

dependencies {
    // grpc
    implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
    implementation 'io.grpc:grpc-services:1.75.0'
    implementation "com.google.protobuf:protobuf-java-util:${dependencyManagement.importedProperties['protobuf-java.version']}"
    implementation("io.grpc:grpc-protobuf:1.75.0") {
        exclude group: 'com.google.protobuf'
    }

    // GraphQl
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'com.tailrocks.graphql:graphql-datetime-spring-boot-starter:6.0.0'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.data:spring-data-commons'
    runtimeOnly "org.postgresql:postgresql:${postgresVersion}"
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"
    annotationProcessor "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"

    // tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${dependencyManagement.importedProperties['protobuf-java.version']}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${dependencyManagement.importedProperties['grpc.version']}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option '@generated=omit'
            }
        }
    }
}

